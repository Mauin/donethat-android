/*
 * In release mode: read password from environment variable if available or console else
 */
def readPassword = { ->
    def runTasks = gradle.startParameter.taskNames
    def env = System.getenv();
    if ('assembleLiveProductionRelease' in runTasks) {
        if (env['RELEASE_KEYSTORE_PW']) {
            return env['RELEASE_KEYSTORE_PW']
        } else {
            return System.console().readLine("\n\$ Enter keystore password: ")
        }
    } else {
        return ""
    }
}

def readKeyaliasPassword = { ->
    def runTasks = gradle.startParameter.taskNames
    def env = System.properties;
    if ('assembleLiveProductionRelease' in runTasks) {
        if (env['RELEASE_KEYALIAS_PW']) {
            return env['RELEASE_KEYALIAS_PW']
        } else {
            return System.console().readLine("\n\$ Enter keyalias password: ")
        }
    } else {
        return ""
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'me.tatarka:gradle-retrolambda:3.2.3'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    maven { url "https://jitpack.io" }
    jcenter()
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    dataBinding {
        enabled = true
    }

    signingConfigs {
        release {
            storeFile file("donethat.keystore")
            keyAlias "donethat"
            storePassword readPassword()
            keyPassword readKeyaliasPassword()
        }
    }

    defaultConfig {
        applicationId "com.mtramin.donethat"
        minSdkVersion 23
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        buildConfigField("String", "donethat_twitter_api_key", "\"" + donethat_twitter_api_key + "\"")
        buildConfigField("String", "donethat_twitter_api_secret", "\"" + donethat_twitter_api_secret + "\"")

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

retrolambda {
    jvmArgs '-noverify'
}

ext.playServicesVersion = '8.3.0'
ext.supportLibraryVersion = '23.1.1'

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    // Workaround needed, since Espresso has a bug
    // see: https://stackoverflow.com/questions/28999124/resolved-versions-for-app-22-0-0-and-test-app-21-0-3-differ
    resolutionStrategy.force "com.android.support:support-annotations:$supportLibraryVersion"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Support libs
    compile "com.android.support:support-v13:$supportLibraryVersion"
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:support-annotations:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:palette-v7:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"

    // Play Services
    compile "com.google.android.gms:play-services-location:$playServicesVersion"
    compile "com.google.android.gms:play-services-maps:$playServicesVersion"

    // HTTP & Networking
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'eu.unicate.android:retroauth:1.0.0'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.5.0'
    provided 'com.bluelinelabs:logansquare-compiler:1.3.3'
    compile 'com.bluelinelabs:logansquare:1.3.3'
    compile 'com.infstory:converter-logansquare-retrofit:1.0.0'

    // Injection
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.google.dagger:dagger:2.0'
    apt 'com.google.dagger:dagger-compiler:2.0'

    compile 'io.reactivex:rxjava:1.0.14'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'net.danlew:android.joda:2.8.1'
    compile 'com.github.bumptech.glide:glide:3.6.0'
    compile 'org.scribe:scribe:1.3.7'
    compile 'io.realm:realm-android:0.85.0'

    // DEMO
    compile 'net._01001111:jlorem:1.1'

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.9.5"
    androidTestCompile 'junit:junit:4.12'
    androidTestCompile 'com.squareup.okhttp:mockwebserver:2.5.0'
    androidTestCompile 'com.android.support.test:runner:0.4'
    androidTestCompile 'com.android.support.test:rules:0.4'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:+'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:+'
}
